//Mega Man X 16 Compiler Demo
//Written by Prads

Image imgLogo = "logo.bin", 200, 175;
Image imgStand = "stand.bin", 40, 48;
Image imgShoot = "shoot.bin", 40, 48;
Image imgDash = "dash.bin", 60, 39;
Image imgFly = "fly.bin", 40, 58;
Image imgStormEagle = "stormEagle.bin", 40, 41;
Image imgBullet = "bullet.bin", 4, 4;
Image imgWin = "win.bin", 66, 100;
Image imgLose = "lose.bin", 88, 73;
Image imgMoon = "moon.bin", 100, 102;

ConstString title = "Mega Man X 16 by Prads";
ConstString pressStart = "Press Start";
ConstString win = "You Win";
ConstString lose = "You Lose";

void main() {
	//Gamepad constants
	const UP = 1; const DOWN = 2; const LEFT = 4; const RIGHT = 8;
	const SELECT = 16; const START = 32; const A = 64; const B = 128;
	
	//Flip constants
	const FLIP_NONE = 0; const FLIP_VERTICAL = 1; const FLIP_HORIZONTAL = 2; const FLIP_BOTH = 3;
	
	//Game scenes constants
	const SCENE_START = 0; const SCENE_PLAY = 1; const SCENE_WIN = 2; const SCENE_LOSE = 3;
	
	//Character glide control
	const GLIDE_UP = 0; const GLIDE_DOWN = 1; const GLIDE_NONE = 2;
	
	//Character Bullet constants
	const BULLET_ACTIVE = 0; const BULLET_INACTIVE = 1;
	
	var scene = SCENE_START;	//Current game scene
	
	const PLAYER_STAND = 0; const PLAYER_SHOOT = 1; const PLAYER_DASH = 2; const PLAYER_FLY = 3;
	var playerX = 10; var playerY = 190;	//x and y coordinates of player
	var playerGlide = GLIDE_NONE;	//player glide control
	var playerSpriteType;	//player sprite control
	var playerBulletX; var playerBulletY; var playerBulletCtrl = BULLET_INACTIVE;
	
	var enemyY = 190;	//y coordinate of enemy
	var enemyGlide = GLIDE_UP;	//enemy glide control
	var enemyBulletX; var enemyBulletY; var enemyBulletCtrl = BULLET_INACTIVE;
	var enemyLife = 5;
	
	//Game's main loop
	while (1) {
		ClearScreen;
		
		//Game Scenes
		if (scene == SCENE_START) {		//Start Scene
			Draw(imgLogo, 60, 10);
			Print(title, 60, 200);
			Print(pressStart, 115, 215);
			if (GetController1 & START) {
				scene = SCENE_PLAY;
			}
		} else if (scene == SCENE_PLAY) {	//In Game Scene
			//Draw Background Moon
			Draw(imgMoon, 100, 50);
		
			//Get gamepad buttons
			if (playerGlide == GLIDE_NONE) {
				playerSpriteType = PLAYER_STAND;
			}
			if (playerGlide == GLIDE_NONE) {
				if(GetController1 & UP)) {
					playerGlide = GLIDE_UP;
					playerSpriteType = PLAYER_FLY;
				}
			} else if (playerGlide == GLIDE_UP) {
				if (playerY > 100) {
					playerY = playerY - 1;
				} else {
					playerGlide = GLIDE_DOWN;
				}
			} else {
				if (playerY < 190) {
					playerY = playerY + 1;
				} else {
					playerGlide = GLIDE_NONE;
				}
			}
			
			if (GetController1 & A) {
				if (playerBulletCtrl == BULLET_INACTIVE) {
					playerBulletCtrl = BULLET_ACTIVE;
					playerBulletX = playerX + 48;
					playerBulletY = playerY + 20;
				}
				playerSpriteType = PLAYER_SHOOT;
			} else if (GetController1 & LEFT) {
				playerX = playerX - 1;
				Flip(FLIP_HORIZONTAL);
				playerSpriteType = PLAYER_DASH;
			} else if (GetController1 & RIGHT) {
				playerX = playerX + 1;
				playerSpriteType = PLAYER_DASH;
			}
			
			//Draw Player Sprite
			if (playerSpriteType == PLAYER_STAND) {
				Draw(imgStand, playerX, playerY);
			} else if (playerSpriteType == PLAYER_SHOOT) {
				Draw(imgShoot, playerX, playerY);
			} else if (playerSpriteType == PLAYER_DASH) {
				Draw(imgDash, playerX, playerY);
			} else {
				Draw(imgFly, playerX, playerY);
			}
			
			//Draw Player Bullet
			if (playerBulletCtrl == BULLET_ACTIVE) {
				playerBulletX = playerBulletX + 3;
				if (playerBulletX >= 320) {
					playerBulletCtrl = BULLET_INACTIVE;
				}
				Draw(imgBullet, playerBulletX, playerBulletY); 
			}
			
			//Enemy Glide
			if (enemyGlide == GLIDE_UP) {
				if (enemyY > 100) {
					enemyY = enemyY - 1;
				} else {
					enemyGlide = GLIDE_DOWN;
				}
			} else {
				if (enemyY < 190) {
					enemyY = enemyY + 1;
				} else {
					enemyGlide = GLIDE_UP;
				}
			}
			
			Flip(FLIP_NONE);
			Draw(imgStormEagle, 280, enemyY);	//draw enemy Sprite
			
			//Draw Enemy Bullet
			if (enemyBulletCtrl == BULLET_ACTIVE) {
				enemyBulletX = enemyBulletX - 3;
				if (enemyBulletX <= 0) {
					enemyBulletCtrl = BULLET_INACTIVE;
				}
				Draw(imgBullet, enemyBulletX, enemyBulletY);
			} else {
				enemyBulletCtrl = Rand & 3;
				if (enemyBulletCtrl == BULLET_ACTIVE) {
					enemyBulletX = 280;
					enemyBulletY = enemyY + 20;
				}
			}
			
			//Player Bullet and Enemy Collision
			if (playerBulletCtrl == BULLET_ACTIVE) {
				if ((playerBulletX < 320) && (playerBulletX > 280) && (playerBulletY > enemyY) && (playerBulletY < enemyY + 40)) {
					enemyLife = enemyLife - 1;
					if (enemyLife == 0) {
						scene = SCENE_WIN;
					}
					playerBulletCtrl = BULLET_INACTIVE;
				}
			}
			//Enemy Bullet and Player Collision
			if (enemyBulletCtrl == BULLET_ACTIVE) {
				if ((enemyBulletX < playerX + 40) && (enemyBulletX > playerX) && (enemyBulletY > playerY) && (enemyBulletY < playerY + 40)) {
					scene = SCENE_LOSE;
				}
			}			
			
			//Making sure this won't happen :P
			if (playerX >= 240) {
				scene = SCENE_LOSE;
			}
		} else if (scene == SCENE_WIN) {	//Win Scene
			Print(win, 130, 30);
			Print(pressStart, 120, 40);
			Draw(imgWin, 126, 55);
		} else {	//Lose Scene
			Print(lose, 130, 30);
			Print(pressStart, 120, 40);
			Draw(imgLose, 126, 55);
		}
	
		//Reset variables and restart game
		if ((scene == SCENE_WIN) || (scene == SCENE_LOSE)) {
			if (GetController1 & START) {
				playerX = 10; playerY = 190;
				playerGlide = GLIDE_NONE;
				playerBulletCtrl = BULLET_INACTIVE;
	
				enemyY = 190;
				enemyGlide = GLIDE_UP;
				enemyBulletCtrl = BULLET_INACTIVE;
				enemyLife = 5;
				
				scene = SCENE_PLAY;
			}
		}		
		
		WaitScreenRefresh;
	}
}